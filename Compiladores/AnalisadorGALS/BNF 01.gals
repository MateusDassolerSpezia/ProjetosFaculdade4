#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mai: [A-Z]
min: [a-z]
dig: [0-9]

//Comentario de linha:
comentLinha: \-\- [^\n]*

//Comentario de bloco:
comentBloco: "{" [^"{""}"]* "}"
#Tokens
id: ({mai}?{min}) ({mai}?{min})* (_{dig}+)?
cInt: ([1-9] {dig}? {dig}? {dig}? {dig}?) | 0
cFloat: (([1-9] {dig}? {dig}? {dig}? {dig}?) | 0) "." (({dig}? {dig}? {dig}? {dig}? [1-9]) | 0)
cString: \" [^\n\"\\]* \"

//Palavras reservadas
pr_add       = id: "add"
pr_and       = id: "and"
pr_begin     = id: "begin"
pr_bool      = id: "bool"
pr_count     = id: "count"
pr_delete    = id: "delete"
pr_do        = id: "do"
pr_elementOf = id: "elementOf"
pr_else      = id: "else"
pr_end       = id: "end"
pr_false     = id: "false"
pr_float     = id: "float"
pr_if        = id: "if"
pr_int 	    = id: "int"
pr_list 	    = id: "list"
pr_not 	    = id: "not"
pr_or 	    = id: "or"
pr_print     = id: "print"
pr_read      = id: "read"
pr_size      = id: "size"
pr_string    = id: "string"
pr_true      = id: "true"
pr_until     = id: "until"

//Simbolos especiais
"+"
"-"
"*"
"/"
"=="
"~="
"<"
">"
"="
"<-"
"("
")"
";"
","

//Reconhecer e ignorar caracteres de formação 
:[\n\t\s]

//Reconhecer e ignorar comentários:
:!{comentBloco}
:!{comentLinha}
#NonTerminals
<programa>
<lista_de_instrucoes>
<lista_de_instrucoes1>
<instrucao>
<declaracao_de_variaveis>
<comando>
<comando1>
<tipo>
<lista_de_identificadores>
<tipo_simples>
<tipo_lista>
<repete_id>
<comando_de_atribuicao1>
<comando_de_manipulacao_de_listas1>
<comando_de_entrada_de_dados>
<comando_de_saida_de_dados>
<comando_de_selecao>
<comando_de_selecao1>
<comando_de_repeticao>
<expressao>
<elemento>
<posicao>
<lista_de_entrada>
<string>
<repete_id_entrada> 
<lista_de_expressoes>
<repete_expressao>
<lista_de_comandos>
<repete_comando>

<expressao_>
<valor> 
<relacional> 
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_> 
<fator>
<fator_>
#Grammar
<programa> ::= #100 pr_begin <lista_de_instrucoes> pr_end #101 ;

<lista_de_instrucoes> ::= <instrucao> ";" <lista_de_instrucoes1> ;
<lista_de_instrucoes1> ::= <lista_de_instrucoes> | î ;
<instrucao> ::= <declaracao_de_variaveis> | <comando> ;

<declaracao_de_variaveis> ::= <tipo> <lista_de_identificadores> #119 ;
<tipo> ::= <tipo_simples> | <tipo_lista> ;
<tipo_simples> ::= pr_bool #120 | pr_int #120 | pr_float #120 | pr_string #120 ;
<tipo_lista> ::= pr_list "(" <tipo_simples> "," cInt ")" ;
<lista_de_identificadores> ::= id #121 <repete_id> ;
<repete_id> ::= "," id #121 <repete_id> | î ;

<comando> ::= id #121 <comando1> | <comando_de_entrada_de_dados> | <comando_de_saida_de_dados> | <comando_de_selecao> | <comando_de_repeticao> ;
<comando1> ::= <comando_de_atribuicao1> | <comando_de_manipulacao_de_listas1> ;

<comando_de_atribuicao1> ::= "=" <expressao> #122 | "<-" <expressao> #122;

<comando_de_manipulacao_de_listas1> ::= pr_add "(" <elemento> "," <posicao> ")" |  pr_delete "(" <posicao> ")" ;
<elemento> ::= <expressao> ;
<posicao> ::= <expressao> ;

<comando_de_entrada_de_dados> ::= pr_read "(" <lista_de_entrada> ")" ;
<lista_de_entrada> ::= <string> id #123 <repete_id_entrada> ;
<string> ::= cString #124 "," | î ;
<repete_id_entrada> ::= "," <string> id #123 <repete_id_entrada> | î ;

<comando_de_saida_de_dados> ::= pr_print "(" <lista_de_expressoes> ")" #118 ;
<lista_de_expressoes> ::= <expressao> #102 <repete_expressao> ;
<repete_expressao> ::= "," <expressao> #102 <repete_expressao> | î ;

<comando_de_selecao> ::=  pr_if <expressao> #125 <lista_de_comandos> <comando_de_selecao1> pr_end #126 ;
<comando_de_selecao1> ::= î | pr_else #127 <lista_de_comandos> ;

<lista_de_comandos> ::= <comando> ";" <repete_comando> ;
<repete_comando> ::= <comando> ";" <repete_comando> | î ;

<comando_de_repeticao> ::= pr_do #128 <lista_de_comandos> pr_until <expressao> #129 ;


<expressao> ::= <valor> <expressao_> ;
<expressao_> ::= î | pr_and <valor> #113 <expressao_> | pr_or <valor> #114 <expressao_> ;
<valor> ::= <relacional> | pr_true #115 | pr_false #116 | pr_not <valor> #117 ;
<relacional> ::= <aritmetica> <relacional_> ;
<relacional_> ::= î | <operador_relacional> #111 <aritmetica> #112 ;
<operador_relacional>::= "==" | "~=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica_> ;
<aritmetica_> ::= î | "+" <termo> #106 <aritmetica_> | "-" <termo> #107 <aritmetica_> ;
<termo> ::= <fator> <termo_> ;
<termo_> ::= î | "*" <fator> #108 <termo_> | "/" <fator> #109 <termo_> ;
<fator> ::= id #130 <fator_> | cInt #103 | cFloat #104 | cString #105 | "(" <expressao> ")" | "+" <fator> | "-" <fator> #110 ;
<fator_> ::= î | pr_count | pr_size | pr_elementOf "(" <expressao> ")" ; 
